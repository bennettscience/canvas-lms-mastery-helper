behavior Sortable

    js
        function sortArray(array, index, asc) {
            let sorted;
            if(asc) {
                sorted = array.sort((a, b) => {
                    valA = a.children[index].innerText.toUpperCase()
                    valB = b.children[index].innerText.toUpperCase()

                    return ( valA < valB ) ? -1 : ( valA > valB ) ? 1 : 0
                })
            } else {
                sorted = array.sort((a, b) => {
                    valA = a.children[index].innerText.toUpperCase()
                    valB = b.children[index].innerText.toUpperCase()

                    return ( valA > valB ) ? -1 : ( valA < valB ) ? 1 : 0
                })
            }
            return sorted
        }
    end

    on click from .sort-trigger
        -- get all the headers and the index of the column
        set headers to <th/> in me
        set parent to target.parentElement.parentElement.parentElement
        -- the target is nested twice, so we have to jump back up to 
        -- the correct parent element to get the column index.
        set col to headers.indexOf(parent)
        
        -- get the table body and the rows as an array
        set tbody to the first <tbody/> in me
        set rowArray to (<tr/> in tbody) as Array
        
        -- if this is a first click on this header
        -- clear the data-ascending attribute on any other header
        if parent @aria-sort is null
            remove @aria-sort from headers
            -- set target.parentElement @aria-sort to 'ascending'
        end

        -- sort the array depending on the ascending header
        if parent @aria-sort is 'ascending'
            set parent @aria-sort to ''
            sortArray(rowArray, col, false)
            put 'Sort A-Z' into .sort-trigger
        else
            set parent @aria-sort to 'ascending'
            sortArray(rowArray, col, true)
            put 'Sort Z-A' into .sort-trigger
        end
        
        -- merge back into the tbody
        for row in rowArray
            tbody.append(row)
        end
    end  
end